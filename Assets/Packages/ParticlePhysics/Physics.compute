#define WARP_SIZE 64
#define MAX_THREAD_GROUPS 1024
#define MAX_X_THREADS (WARP_SIZE * MAX_THREAD_GROUPS)
#define G_CONST -9.81
#pragma kernel UploadVelocity
#pragma kernel UploadPosition
#pragma kernel UploadLife
#pragma kernel SimulateVelocity
#pragma kernel SimulatePosition
#pragma kernel SimulateLife
#pragma kernel SolveWallCollision
#pragma kernel SolveParticleCollision
#pragma kernel ClampVelocity



static const float2 G = float2(0, G_CONST);

struct Wall {
	float2 n;
	float2 t;
	float dn;
	float dt;
	float w;
	float h;
};

static const float particleMass = 0.01;
static const float impulsiveMassCoeff = particleMass / (particleMass + particleMass);
static const float distanceEpsilon = 0.01;
static const float penetrationBias = 0.01;
static const float penetrationSlop = 0.02;
static const float friction = 0.2;
static const float maxSpeed = 10.0;
float dt;
float elastics;
float particleRadius;
int uploadOffset;
int uploadLength;
int wallCount;
RWStructuredBuffer<float2> VelocitiesCurr;
RWStructuredBuffer<float2> VelocitiesNext;
RWStructuredBuffer<float2> Positions;
RWStructuredBuffer<float> Lifes;
StructuredBuffer<float2> Uploader;
StructuredBuffer<float> UploaderFloat;
StructuredBuffer<Wall> Walls;



[numthreads(WARP_SIZE,1,1)]
void UploadVelocity (uint3 id : SV_DispatchThreadID) {
	uint velocitiesCapasity, stride;
	VelocitiesCurr.GetDimensions(velocitiesCapasity, stride);
	
	uint i = dot(id.xy, uint2(1, MAX_X_THREADS));
	uint j = (i + uploadOffset) % velocitiesCapasity;
	if (i >= (uint)uploadLength)
		return;
	VelocitiesCurr[j] = Uploader[i];
}
[numthreads(WARP_SIZE,1,1)]
void UploadPosition (uint3 id : SV_DispatchThreadID) {	
	uint positionsCapasity, stride;
	Positions.GetDimensions(positionsCapasity, stride);
	
	uint i = dot(id.xy, uint2(1, MAX_X_THREADS));
	uint j = (i + uploadOffset) % positionsCapasity;
	if (i >= (uint)uploadLength)
		return;
	Positions[j] = Uploader[i];
}
[numthreads(WARP_SIZE,1,1)]
void UploadLife(uint3 id : SV_DispatchThreadID) {
	uint lifesCapacity, stride;
	Lifes.GetDimensions(lifesCapacity, stride);
	
	uint i = dot(id.xy, uint2(1, MAX_X_THREADS));
	uint j = (i + uploadOffset) % lifesCapacity;
	if (i >= (uint)uploadLength)
		return;
	Lifes[j] = UploaderFloat[i];
}
[numthreads(WARP_SIZE,1,1)]
void SimulateVelocity(uint3 id : SV_DispatchThreadID) {
	uint i = dot(id.xy, uint2(1, MAX_X_THREADS));
	VelocitiesNext[i] = VelocitiesCurr[i] + dt * G;
}
[numthreads(WARP_SIZE,1,1)]
void SimulatePosition(uint3 id : SV_DispatchThreadID) {
	uint i = dot(id.xy, uint2(1, MAX_X_THREADS));
	Positions[i] += dt * VelocitiesCurr[i];
}
[numthreads(WARP_SIZE,1,1)]
void SimulateLife(uint3 id : SV_DispatchThreadID) {
	uint i = dot(id.xy, uint2(1, MAX_X_THREADS));
	Lifes[i] = max(0.0, Lifes[i] - dt);
}
[numthreads(WARP_SIZE,1,1)]
void SolveWallCollision(uint3 id : SV_DispatchThreadID) {
	uint i = dot(id.xy, uint2(1, MAX_X_THREADS));
	float2 x = Positions[i];
	float2 v = VelocitiesCurr[i];
	
	float2 p = 0;
	float cpenet = penetrationBias / dt;
	for (int j = 0; j < wallCount; j++) {
		Wall wall = Walls[j];
		float v0n = dot(wall.n, v);
		float dn = dot(wall.n, x) - wall.dn;
		float dt = dot(wall.t, x) - wall.dt;
		if (abs(dn) > (wall.h + particleRadius) || abs(dt) > (wall.w + particleRadius))
			continue;
		float penet = cpenet * max(0.0, (wall.h + particleRadius - penetrationSlop) - dn);
		float pn = max(0.0, -(1.0 + elastics) * v0n + penet);
		float v0t = dot(wall.t, v);
		float pt = clamp(-v0t, -friction * pn, friction * pn);
		p += pn * wall.n + pt * wall.t;
	}
	
	VelocitiesNext[i] = v + p;
}
[numthreads(WARP_SIZE,1,1)]
void SolveParticleCollision(uint3 id : SV_DispatchThreadID) {
	uint lifesCapacity, stride;
	Lifes.GetDimensions(lifesCapacity, stride);
	
	uint i = dot(id.xy, uint2(1, MAX_X_THREADS));
	float ti = Lifes[i];
	float2 xi = Positions[i];
	float2 vi = VelocitiesCurr[i];
	if (ti <= 0.0)
		return;
	
	float2 p = 0;
	float colDist = 2.0 * particleRadius;
	float colMinDist = -particleRadius + distanceEpsilon;
	float cpenet = penetrationBias / dt;
	for (uint j = 0; j < lifesCapacity; j++) {
		float tj = Lifes[j];
		float2 xj = Positions[j];
		float2 dp = xi - xj;
		float d = length(dp) - colDist;
		if (i == j || tj <= 0.0 || 0 <= d || d <= colMinDist)
			continue;
		
		float2 vj = VelocitiesCurr[j];
		float2 v = vi - vj;
		float2 n = normalize(dp);
		float v0n = dot(n, v);
		float penet = cpenet * max(0.0, -(d + penetrationSlop));
		float pn = max(0.0, -impulsiveMassCoeff * (1.0 + elastics) * v0n + penet);
		float2 t = float2(n.y, -n.x);
		float v0t = dot(t, v);
		float pt = clamp(-v0t, -friction * pn, friction * pn);
		p += pn * n + pt * t;
	}
	VelocitiesNext[i] = vi + p;
}
[numthreads(WARP_SIZE,1,1)]
void ClampVelocity(uint3 id : SV_DispatchThreadID) {
	uint i = dot(id.xy, uint2(1, MAX_X_THREADS));
	float2 v = VelocitiesCurr[i];
	float a = length(v);
	VelocitiesNext[i] = clamp(a, -maxSpeed, maxSpeed) * normalize(v);
}
